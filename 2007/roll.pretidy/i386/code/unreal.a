; INCLUDE real.h

use16
unrealmode:
        cli
        call enableA20                  ; --- A20 gate on now
        sti

        pushfd
        push ds, es, fs, gs

        mov edx, ds
        shl edx, 4

        add d[unGDT+2], edx
        add d[ipd], edx

        mov w[oldip], offset restsegs
        mov w[oldip+2], cs

        pushd 0000h
        popfd

        lgdt cs:[unGDT]

        mov eax, cr0
        or eax, 1
        mov cr0, eax

        ; jmp far kCS:setsegs
        db 066h, 0eah
ipd:    dd offset setsegs
        dw 08h                  ; kCS

setsegs:
        mov bx, 10h
        mov ds, bx
        mov es, bx
        mov fs, bx
        mov gs, bx

        and ax, 0fffeh          ; not PE
        mov cr0, eax

        ; jmp far oldCS:restsegs
        db 0eah
oldip:  dw offset restsegs, 0                         ; oldip, oldcs

restsegs:
        pop gs, fs, es, ds
        popfd
        ret                             ; to whence we came

enableA20:                              ; Enable gate A20
        call enableA20o1
        jnz short enableA20done
        mov al,0d1h
        out 64h,al
        call enableA20o1
        jnz short enableA20done
        mov al,0dfh
        out 60h,al
enableA20o1:
        mov ecx,20000h
enableA20o1l:
        jmp short $+2
        in al,64h
        test al,2
        loopnz enableA20o1l
enableA20done:
        ret

.align 8
;       SegDesc b1,Base, Limit[0..15], Type1, T2[4..7], Limit[16..19]
unGDT   dw      0ffffh
        dd      offset unGDT
        dw      0
        SegDesc  00, 0000h,0ffff,10011010xb,8fh, 0      ; kCS  (08h) CPL0
        SegDesc  00, 0000h,0ffff,10010010xb,8fh, 0      ; kDS  (10h)

